/*
 * Gradle 0.9 build file.
 * 
 * - Download Gradle from <http://www.gradle.org/>
 * - Execute gradle build
 * 
 * TODO: Zip / Tar archives, run TMAPI.AllTests
 *
 */
group = 'org.tinytim'
version = '2.0.1'
bundleVersion = version

apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'maven'

gradle.taskGraph.whenReady {taskGraph ->
   if (!taskGraph.hasTask(':release')) {
       version += '-SNAPSHOT'
   }
}

repositories {
    mavenCentral()
    mavenRepo urls: 'http://www.tmapi.org/maven-repository/'
    mavenRepo urls: 'http://www.tmapi.org/maven-repository/snapshots/'
}

dependencies {
    compile group: 'org.tmapi', name: 'tmapi', version: '2.0.2'
    compile files('lib/semagia-mio-0.9.5.jar')

    testCompile group: 'junit', name: 'junit', version: '4.+'
    testCompile group: 'org.tmapi', name: 'tmapi-tests', version: '2.0.3-SNAPSHOT'
}

jar {
    manifest {
      attributes 'Implementation-Title': 'tinyTiM', 'Implementation-Version': version
      instruction 'Bundle-Description', 'tinyTiM - A tiny Topic Maps engine'
      instruction 'Import-Package', 'org.tmapi.core, org.tmapi.index'
      instruction 'Export-Package', 'org.tinytim.mio, org.tinytim.voc, org.tinytim.utils'
    }
}

artifacts {
    archives jar
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/maven-repo/")
    }
}
