/*
 * Gradle 0.9 build file.
 * 
 * - Download Gradle from <http://www.gradle.org/>
 * - Execute gradle build
 * 
 * TODO: Zip / Tar archives
 *
 */
group = 'org.tinytim'
version = '2.0.1'
bundleVersion = version

project.projectName = 'tinyTiM'
project.projectDescription = 'Topic Maps engine'
project.vendorName = 'tinyTiM project'
project.vendorURL = 'http://tinytim.sourceforge.net/'
project.licenseName = 'Apache Software License, Version 2.0'
project.licenseURL = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
project.projectURL = 'http://tinytim.sourceforge.net/'

apply plugin: 'java'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply from:'https://github.com/breskeby/gradleplugins/raw/master/emmaPlugin/emma.gradle'

defaultTasks 'clean', 'build'

//TODO: Remove me once <http://jira.codehaus.org/browse/GRADLE-1079> is fixed
eclipseClasspath.variables = [:]

gradle.taskGraph.whenReady {taskGraph ->
   if (!taskGraph.hasTask(':release')) {
       version += '-SNAPSHOT'
   }
}

REP_TMAPI = 'http://www.tmapi.org/maven-repository/'
REP_TMAPI_SNAPSHOTS = 'http://www.tmapi.org/maven-repository/snapshots/'
REP_SEMAGIA_SNAPSHOTS = 'http://repository.semagia.com/snapshots/'

repositories {
    mavenCentral()
    mavenRepo urls: REP_TMAPI
    mavenRepo urls: REP_TMAPI_SNAPSHOTS
    mavenRepo urls: REP_SEMAGIA_SNAPSHOTS
}

configurations {
    compiledep
}

dependencies {
    emma 'emma:emma:2.0.5312', 'emma:emma_ant:2.0.5312'

    compile 'org.tmapi:tmapi:[2.0.2,)'

    compiledep 'com.semagia.mio:mio-core:0.9.6-SNAPSHOT'

    testCompile 'junit:junit:4.8.1',
                'org.tmapi:tmapi:2.0.3-SNAPSHOT',
                'org.tmapi:tmapi-tests:2.0.3-SNAPSHOT',
                'com.semagia.mio:mio-core-tests:0.9.6-SNAPSHOT',
                'com.semagia.mio:mio-core:0.9.6-SNAPSHOT'
}

eclipseClasspath.downloadSources = false
eclipseClasspath.plusConfigurations.add configurations.compiledep

sourceSets {
    main {
      compileClasspath = sourceSets.main.compileClasspath + configurations.compiledep
    }
}

test.doFirst {
  ant {
    unzip(src: configurations.testCompile.find { f -> f.name.startsWith('tmapi-test') },
          dest: "$buildDir/classes/test") {
        exclude '**/TestTopicMapSystemFactory*'
        exclude '**/TestRFC3986*'
    }
  }
}

jar {
    manifest {
      attributes 'Implementation-Title': 'tinyTiM', 'Implementation-Version': version
      instruction 'Bundle-Description', 'tinyTiM - A tiny Topic Maps engine'
      instruction 'Import-Package', 'org.tmapi.core, org.tmapi.index'
      instruction 'Export-Package', 'org.tinytim.mio, org.tinytim.voc, org.tinytim.utils'
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/maven-repo/")
        
        pom.project {
            name project.projectName
            description project.projectDescription
            url project.projectURL
            
            organization {
                name project.vendorName
                url project.vendorURL
            }
            licenses {
               license {
                  name project.licenseName
                  url project.licenseURL
               }
            }
            repositories {
               repository {
                  id 'tmapi'
                  url REP_TMAPI
               }
               repository {
                  id 'tmapi-snapshot'
                  url REP_TMAPI_SNAPSHOTS
               }
               repository {
                  id 'semagia-snapshots'
                  url REP_SEMAGIA_SNAPSHOTS
               }
            }
        }
    }
}
